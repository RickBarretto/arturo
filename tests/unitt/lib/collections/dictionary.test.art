import {unitt}!

suite "storage and access" [

    suite "simple dictionary" [
        person: #[
            name: "John"
            surname: "Doe"
            age: 35
        ]

        test "should store all fields" [
            assert -> "John" = person\name
            assert -> "Doe" = person\surname
            assert -> 35 = person\age
        ]
    ]

    suite "nested dictionary" [
        resident: #[
            fullname: "John Doe"
            location: #[
                country: "Brazil"
                city: "São Paulo"
            ]
        ]

        test "should access 1-depth values" [
            expectedLocation: #[country: "Brazil" city: "São Paulo"]

            assert -> "John Doe" = resident\fullname
            assert -> expectedLocation = resident\location
        ]

        test "should access n-depth values" [
            assert -> "Brazil" = resident\location\country
            assert -> "São Paulo" = resident\location\city
        ]

    ]
]

suite "scope and evaluation" [

    test "should read out-of-scope variables" [
        country: "Spain"    ; out-of-scope variable

        resident: #[
            fullname: "John Doe"
            country: country
            city: "Madrid"
        ]

        assert -> country = resident\country
    ]

    test "should not alter outside values with same label" [
        country: "Spain"
        entity: "EU"

        location: #[
            continent: "Europe"
            country: "Greece"
            entity: entity
        ]

        assert -> "EU" = entity
        assert -> "Spain" = country
        assert -> "Greece" = location\country
    ]

]